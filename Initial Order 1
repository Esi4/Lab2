             T 118[end] S [начало]
             T 5 S  [\]
             T 0 S  [| очищаем рабочие ячейки ] 
[count]      T 1 S  [|]
             T 4 S  [/]
             A 112[len] S [\ записываем в раб. ячейку длину массива(i)]
             T 2 S        [/]
             A 112[len] S [\ записываем в раб. ячейку длину массива(j)]
             T 3 S        [/] 
             A 114[N0 adr] S [записываем в акк. адрес N0]
             L 0 L [сдвиг на 1 бит влево]
             A 52[<w2>] S [прибавляем к инструкции]
             T 52[<w2>] S [обновляем инструкцию]
[w1]         A 114[N0] S  [записываем в акк. адрес N0]
             L 0 L [сдвиг на 1 бит влево]
             A 58[<w3>] S [прибавляем к инструкции]
             T 58[<w3>] S [обновляем инструкцию]
[loop_for]   A 2 S [помещаем i в акк.]
             S 113[1] S [вычитаем 1]
             G 109[<exit>] S [если результат отрицательный - завершаем программу]
             T 2 S [возвращаем i в раб. ячейку]
[w2]         A 0 S [\запись N в 4 ячейку]
             T 4 S [/]
[loop_for2]  A 3 S              [\]
             S 113[1] S         [| уменьшение j с проверкой на положительность]
	     G 78[<end loop>] S [|]
             T 3 S              [/]
[w3]         A 0 S  [запись в аккум M]  
             S 4 S  [M-N]
             E 66[<f1>] S [проверка результата, если N<M, переходим в 66 ячейку]
[f2]         T 0 S [обнуляем аккум]
             A 1 S       [помещаем в аккум count]
             A 113[1] S  [добавляем 1]
             T 1 S       [обновляем count]
             E 72[<f3>] S [переход к обновлению цикла]
[f1]         S 113[1] S [вычитаем 1]
             G 72[<f3>] S [проверка на N=M]
	     T 0 S [обновляем аккум]
	     A 1 S [помещаем в аккум count]
	     S 113[1] S [вычитаем 1]
	     T 1 S [переход к обновлению цикла]
[f3]	     T 0 S      [обнуляем аккум]
             A 113[1] S   [\]
             L 0 L        [| обновляем инструкцию]
             A 58[<w3>] S [|]
             T 58[<w3>] S [/]
             E 54[<loop_for2>] S [переход в начало цикла]
[end loop]   T 0 S [обнуляем аккум]
             A 1 S [загружаем count]
             G 86[<f5>] S [проверка count<0]
             S 113[1] S [вычитаем 1]
             G 106[<f6>] S [проверка count=0]
             S 113[1] S [вычитаем 1]
             G 89[c1] S [проверка count=1]
             E 95[<f4>] S [иначе топаем в f4]
[f5]         A 113[1] S [прибавляем к count 1]
             E 89[<c1>] S [проверка count=-1]
             G 95[<f4>] S [иначе топаем в f4]
[c1]         T 0 S [обновляем аккум]
             A 4 S [загружаем N в аккум]
             R 1 L [делим на 2]
             A 5 S [прибавляем значения из 5 ячейки]
             T 5 S [записываем в 5 ячейку]
             E 95[<f4>] S [топаем в f4]
[f4]         T 0 S [обновляем аккум]
             A 113[1] S     [\]
             L 0 L          [| обновляем инструкции]
             A 52[<w2>] S   [|]
             T 52[<w2>] S   [|]
             A 111[ref] S   [|]
             T 58[<w3>] S   [|]
             T 1 S          [/]
             A 112[len] S   [\ обновляем j]
             T 3 S          [/]
             E 44[<w1>] S   [начинаем цикл по-новой]
[f6]         T 0 S          [обнуляем аккум]
             A 4 S          [загружаем N в аккум]
             T 5 S          [записываем N в 5 ячейку]
[exit]       O 0 S          [останов] 
             Z 0 S
[consts]     A 0 S [ref]   [инструкция для обновления цикла]
             P 1 L [len]   [длина массива]
             P 0 L [1]     [1]
             P 57 L [adr]  [адрес N0]
             P 4 L         [\] 
             P 8 S         [| массив]
             P 5 S         [/] 

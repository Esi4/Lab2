T 56[start] K [стартуем]
G K
[0]			A 3 F  [пролог: формируем код инструкции возврата в Acc]
[1]			T 71[exit] @ [пролог: записываем инструкцию возврата ]
[2]			A 7 F	[загружаем адрес N0]
[3]			A 75[r1adr] @ [прибавляем код инструкции]
[4]			T 14[<w2>] @ [обновляем код инструкции]
[5]			A 2 F [\ загрузка сформированой интрукции ]
[6]			T 8 F [/]
[7]  [w1]  	        A 7 F	      [\]	
[8]		     	A 76[r2adr] @ [| обновления кода инструкции для w3]
[9]		        T 20[<w3>] @  [/]
[10] [loop_for]	        A 2 F [помещаем i в акк.]
[11]			S 73[1] @ [вычитаем 1] 
[12]			G 71[<exit>] @ [если результат отрицательный - завершаем программу]
[13]			T 2 F [возвращаем i в раб. ячейку]
[14] [w2]		A 0 F [\запись N в 4 ячейку]
[15]			T 4 @ [/]
[16] [loop_for2]        A 1 F
[17]			S 73[1] @        [\]
[18]			G 40[end_loop] @ [| уменьшение j с проверкой на положительность]
[19]			T 1 F		 [|]
[20] [w3]		A 0 F	         [/]		
[21]			S 4 @	[запись в аккум M]
[22]			E 28[<f1>] @ [M-N] [проверка результата, если N<M, переходим в f1]
[23] [f2]		T 0 F [обнуляем аккум]
[24]			A 5 F [помещаем в аккум count]
[25]			A 73[1] @ [добавляем 1]
[26]			T 5 F [обновляем count]
[27]			E 34[<f3>] @ [переход к обновлению цикла]
[28] [f1]		S 73[1] @ [вычитаем 1]
[29]			G 34[<f3>] @ [проверка на N=M]
[30]			T 0 F [обновляем аккум]
[31]			A 5 F [помещаем в аккум count]
[32]			S 73[1] @ [вычитаем 1]
[33]			T 5 F [переход к обновлению цикла]
[34] [f3]		T 0 F [обнуляем аккум]
[35]			A 73[1] @    [\]
[36]			L 0 D	     [| обновляем инструкцию]		
[37]			A 20[<w3>] @ [|]
[38]			T 20[<w3>] @ [/]
[39]			E 16[<loop_for2>] @ [переход в начало цикла]
[40] [end_loop]         T 0 F [обнуляем аккум]
[41]			A 5 F [загружаем count]
[42]			G 48[<f5>] @ [проверка count<0]
[43]			S 73[1] @ [вычитаем 1]
[44]			G 68[<f6>] @ [проверка count=0]
[45]			S 73[1] @ [вычитаем 1]
[46]			G 51[<c1>] @ [проверка count=1]
[47]			E 57[<f4>] @ [иначе топаем в f4]
[48] [f5]		A 73[1] @ [прибавляем к count 1]
[49]			E 51[<c1>] @ [проверка count=-1]
[50]			G 57[<f4>] @ [иначе топаем в f4]
[51] [c1]   	        T 0 F [обновляем аккум]
[52]			A 5 F [загружаем N в аккум]
[53]			R 1 D [делим на 2]
[54]			A 6 F [прибавляем значения из 6 ячейки]
[55]			T 6 F [записываем ответ]
[56]			E 57[<f4>] @ [топаем в f4]
[57] [f4] 		T 0 F [обновляем аккум]
[58]			A 73[1] @ 	[\]
[59]			L 0 D		[|]
[60]			A 14[<w2>] @	[| обновляем инструкции]
[61]			T 14[<w2>] @	[|]
[62]			A 74[adr] @	[/]
[63]			T 7 F		
[64]			T 5 F
[65]			A 8 [len] F [\ обновляем j]
[66]			T 1 F	    [/]	
[67]			E 7 [<w1>] @ [начинаем цикл по-новой]
[68] [f6]		T 0 F [обнуляем аккум]
[69]			A 5 F [загружаем N в аккум]
[70]			T 6 F [записываем ответ]
[71] [exit]		T 0 F [обнуляем аккум]
[72]			E 0 F [останов]
[73]			P 0 D	  [1]
[74]			P 14 F  [adr]
[75]			A 0 F [r1adr]
[76] 			A 0 F [r2adr]



GK
[0] X 0 F
[1] T 0 F
[2] A 12[adr] @ [адрес массива]
[3] T 7 F	[запись в 7 ячейку]
[4] T 5 F	[обнуление count]
[5] A 13[len] @ [i]
[6] T 2 F
[7] A 13[len] @ [j]
[8] T 1 F
[9] A 9 @  [\ вызов]
[10]G 56 F [/ подпрограммы]
[11]Z 0 F [останов]
[12]P 14 @ [адрес N0]
[13]P 1 D [len] 
[array:] [сам массив]
[14] P 4 D
[15] P 8 F
[16] P 5 F

EZ PF
